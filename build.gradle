buildscript {
    ext.kotlin_version = "1.3.72"
    ext.jvm_version = "1.8"
    ext.junit4_version = "4.12"
    ext.hamcrest_version = "2.1"
    ext.commons_math3_version = "3.6.1"
    ext.kotson_version = "2.5.0"

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

plugins {
    id("maven-publish")
    id("org.jetbrains.kotlin.jvm") version "$kotlin_version"
}

// Common configuration
allprojects {

    apply(plugin: "kotlin")
    apply(plugin: "maven-publish")

    group = "jumpaku"
    version = "2.0.0-alpha-SNAPSHOT"

    repositories {
        mavenCentral()
        jcenter()
    }

    tasks.named("compileKotlin") {
        kotlinOptions.jvmTarget = jvm_version
    }
    tasks.named("compileTestKotlin") {
        kotlinOptions.jvmTarget = jvm_version
    }

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

        api("org.apache.commons:commons-math3:$commons_math3_version")
        api("com.github.salomonbrys.kotson:kotson:$kotson_version")
    }


    // Publishing
    /**
     * To run uploadArchives,  ./gradlew publish -Pargs="`user id of nexus.jumpaku.net` `password`"
     */
    publishing {
        def (repositoryUsername, repositoryPassword) = (project?.findProperty("args") as String)?.split(/\s+/) ?: []
        def repositoryUrl = "https://nexus.jumpaku.net/repository/" +
                ((version.endsWith("SNAPSHOT")) ? "maven-snapshots" : "maven-releases")
        repositories {
            maven {
                url = uri(repositoryUrl)
                credentials {
                    username = repositoryUsername ?: "UNKOWN"
                    password = repositoryPassword ?: "UNKOWN"
                }
            }
        }
        publications {
            create("release", MavenPublication) {
                from(components["kotlin"])
            }
        }
    }
}

project(":jumpaku-commons") {
}

project(":jumpaku-commons-test") {

    dependencies {
        implementation(project(":jumpaku-commons"))
        implementation("org.hamcrest:hamcrest:$hamcrest_version")
        implementation("junit:junit:$junit4_version")
    }
}
